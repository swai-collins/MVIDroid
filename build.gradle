// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.50'
    ext.mvikotlin_group_id = 'com.arkivanov.mvikotlin'
    ext.mvikotlin_version = '2.0.0'

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/badoo/maven" }
    }
}

void setupMultiplatformProject(Project project) {
    project.apply plugin: 'org.jetbrains.kotlin.multiplatform'

    project.group = mvikotlin_group_id
    project.version = mvikotlin_version

    project.kotlin {
        sourceSets {
            commonMain {
                dependencies {
                    implementation kotlin('stdlib-common')
                }
            }

            commonTest {
                dependencies {
                    implementation kotlin('test-common')
                    implementation kotlin('test-annotations-common')
                }
            }
        }
    }

    setupAndroidTarget(project)
    setupJvmTarget(project)
    setupLinuxX64Target(project)

    project.kotlin {
        sourceSets {
            jsNativeCommonMain.dependsOn commonMain
            jsNativeCommonTest.dependsOn commonTest

            jvmCommonMain.dependsOn commonMain
            jvmCommonTest.dependsOn commonTest

            jvmMain.dependsOn jvmCommonMain
            jvmTest.dependsOn jvmCommonTest

            androidMain.dependsOn jvmCommonMain
            androidTest.dependsOn jvmCommonTest

            nativeCommonMain.dependsOn jsNativeCommonMain
            nativeCommonTest.dependsOn jsNativeCommonTest

            linuxX64Main.dependsOn nativeCommonMain
            linuxX64Test.dependsOn nativeCommonTest
        }
    }
}


private void setupAndroidTarget(Project project) {
    project.apply plugin: 'com.android.library'

    project.android {
        buildToolsVersion '28.0.3'
        compileSdkVersion 28

        defaultConfig {
            minSdkVersion 14
        }
    }

    project.kotlin {
        targets.fromPreset(presets.android, 'android')

        sourceSets {
            androidMain {
                dependencies {
                    implementation kotlin('stdlib')
                }
            }

            androidTest {
                dependencies {
                    implementation kotlin('test-junit')
                }
            }
        }

        android {
            publishLibraryVariants('release', 'debug')
        }
    }
}

private void setupJvmTarget(Project project) {
    project.kotlin {
        targets.fromPreset(presets.jvm, 'jvm')

        sourceSets {
            jvmMain {
                dependencies {
                    implementation kotlin('stdlib')
                }
            }

            jvmTest {
                dependencies {
                    implementation kotlin('test-junit')
                }
            }
        }
    }
}

private void setupLinuxX64Target(Project project) {
    project.kotlin {
        targets {
            fromPreset(presets.linuxX64, 'linuxX64')
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
